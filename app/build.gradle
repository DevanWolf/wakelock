apply plugin: 'com.android.application'

static def gitSha() {
    return 'git rev-parse --short HEAD'.execute().text.trim()
}

def versionRewrite = 2
def versionRelease = 3

android {
    compileSdkVersion 7
    buildToolsVersion "25.0.2"

    defaultConfig {
        minSdkVersion 7
        targetSdkVersion 7

        versionCode versionRewrite * 10000 + versionRelease
        versionName "${versionRewrite}.${versionRelease}"
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles 'proguard.cfg'
        }
        applicationVariants.all { variant ->
            if (variant.buildType.name == "release") {
                variant.outputs.each { output ->
                    def file = output.outputFile
                    output.outputFile = new File(file.parent, applicationId + "-v" + versionName + "-" + versionCode + "-" + gitSha() + ".apk")
                }
            }
        }
    }
    productFlavors {
        free {
            applicationId "eu.thedarken.wl"
            versionNameSuffix "-f"
        }
        donate {
            applicationId "eu.thedarken.wldonate"
            versionNameSuffix "-d"
        }
    }

}
